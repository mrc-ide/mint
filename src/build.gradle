buildscript {
    ext.kotlin_version = '1.9.21'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.18' apply false
    id 'io.gitlab.arturbosch.detekt' version "1.23.4"
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: "kotlin-spring"
    apply plugin: "io.gitlab.arturbosch.detekt"

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.4"
    }
    detekt {
        config = files("$rootDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
        reports {
            // these reports are generated in the build/reports/detekt directory
            txt.enabled = true
        }
    }
    tasks.detekt.jvmTarget = "1.8"

    jacoco {
        toolVersion = "0.8.11"
        reportsDirectory = file("$projectDir/coverage")
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
        }
    }

    jar {
        enabled true
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
    }

    configurations {
        all*.exclude module : 'spring-boot-starter-logging'
    }

    group = 'org.imperial.mrc'
    sourceCompatibility = '1.8'

}
